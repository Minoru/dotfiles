-- General idea here is that GUI program names starts with capital
aliases (
      "chromium"       -> "Chromium"
    , "Navigator"      -> "Firefox"
    , "anki"           -> "Anki"
    , "gl"             -> "MPV"
    )
-- A rule that probably everybody wants. Being inactive for over a minute
-- causes this sample to be ignored by default.
$idle > 60 ==> tag inactive,

-- Tag some most used programs
current window ($program == "urxvt" &&
    $title =~ /^(|\[[\w-_]+\] )(|\([\w-_]+\) )vim(| .+)$/ )
    ==> tag Program:vim,
current window ($program == "urxvt" &&
    $title =~ /^(|\[[\w-_]+\] )(|\([\w-_]+\) )irssi(| .+)$/ )
    ==> tag Program:irssi,
current window (
    $program == "urxvt" &&
    $title =~ /^(|\[[\w-_]+\] )(|\([\w-_]+\) )mcabber(| .+)$/ )
    ==> tag Program:mcabber,
current window ($program == "urxvt" &&
    $title =~ /^(|\[[\w-_]+\] )(|\([\w-_]+\) )mutt(| .+)$/ )
    ==> tag Program:mutt,

-- when tmux is running, zsh includes the session name in the title of the
-- window. We conclude that I'm using tmux when we can observe the session name
-- in the terminal window's title
current window ($program == "urxvt" &&
    $title =~ /^(|\[[\w-_]+\] )(|\([\w-_]+\) ).*$/ )
    ==> tag Program:tmux,

-- Activities
current window $program == "anki"
    ==> tag Activity:anki,
current window ($program == "urxvt" &&
    $title =~ /^(|\[[\w-_]+\] )(|\([\w-_]+\) )irssi(| .+)$/ )
    ==> tag Activity:chatting,
current window (
    $program == "urxvt" &&
    $title =~ /^(|\[[\w-_]+\] )(|\([\w-_]+\) )mcabber(| .+)$/ )
    ==> tag Activity:chatting,
current window (
    $program == ["chromium", "Navigator"] &&
    ! ($title =~ /(^| - )YouTube - (Chromium|Vimperator)$/) )
    ==> tag Activity:web_browsing,
current window (
    $program == "urxvt" &&
    $title =~ /ELinks$/ )
    ==> tag Activity:web_browsing,
current window (
    $program == ["chromium", "Navigator"] &&
    $title =~ /(^| - )YouTube - (Chromium|Vimperator)$/ )
    ==> tag Activity:watching_videos,
current window $program == "gl"
    ==> tag Activity:watching_videos,
current window ($program == "urxvt" &&
    $title =~ /^(|\[[\w-_]+\] )(|\([\w-_]+\) )nethack$/ )
    ==> tag Activity:nethack,
current window ($program == "urxvt" &&
    $title =~ /^(|\[[\w-_]+\] )(|\([\w-_]+\) )vim(| .+)$/ )
    ==> tag Activity:text_editing,
current window ( $program == "urxvt" &&
    $title =~ /^(|\[[\w-_]+\] )(|\([\w-_]+\) )zsh(| .+)$/ )
    ==> tag Activity:terminal,
current window $program == "gimp"
    ==> tag Activity:graphics,
current window $program == "luminance-hdr"
    ==> tag Activity:graphics,
-- Catch 'em all rule for activity in terminal emulator
current window $program == "urxvt"
    ==> tag Activity:terminal,
-- Debug rule, used as catch-all to detect some new patterns  and uncatched apps
-- tag Activity:other_$current.program___$current.title,

-- Simple rule that just tags the current program
tag Program:$current.program,

-- Another simple rule, just tags the current desktop (a.k.a. workspace)
tag Desktop:$desktop,
